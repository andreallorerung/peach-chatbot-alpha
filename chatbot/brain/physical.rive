! version = 2.0

//
! array respiratory = respir|breath|gasp|wheez|asthm
//
! array urinary = urin|pee|piss
//                                                              |fecal matter|feces|
! array constipation = constip|stool|defec|poop|shit|crap|dung|excr|fec mat|fec|diarrhoe|diarrhe|loos stool
//                        |feed|                |hunger|appetence
! array eating = eat|appetit|fee|injest|hav meal|hung|appet
//
! array indigestion = indigest

//              sore mouth|   |aching mouth|
! array mouth = sor mou|dry mou|ach mou|inflam mou|mou inflam|mou pain
//                                              |heave|
! array nausea = nause|vomit|queasy|regurgit|sick|heav|puk|throw up
//
! array sleeping = sleep|sleepy|nightm|dorm|snooz|bad dream
//
! array fatigue = fatigu|lethargy|letharg|weary|exhaust|feebl|tir|sleepy|faint
//                              |belly|
! array swelling = swel|limb|tummy|bel|abdom|arm|leg
//
! array fever = fev|high temp|burn up

//
! array walking = walk|get around
//
! array tingling = tingl|goos bump
//
! array pain = pain|ach|agony|burn|cramp|il|injury|irrit|sick|hurt
//                          |sweat|
! array hotflushes = hot flush|swe|clammy
//
! array skin = dry skin|itch|itchy skin|sor skin
//                                             |damage|lesion||incision|section|opening
! array woundcare = wound care|aft surgery|surgery|dam|les|pain|incid|sect|op|cut

//
! array weight = weight chang|mass chang|diff weight|gain|los|loss|lost
//                                                       |concentration|
! array concentration = mem|recollect|rememb|thought|recal|cont|focusing|study|read
//                                |image|
! array sensory = sens|sensual|see|im|vis|look|view|watch|star|gaz|glar|read|ey|hear|list|audit|aur|ear|audiov|hallucin|tast|flavo|sweet|bit|tongu
//                                                                 |vocal|          |tone|
! array speaking = speak|talk|say|shout|tel|voic|whisp|convers|mumbl|voc|sound|speech|ton|yel|song|sing
//                         |image|presentation|countenace       |complexion|pale
! array appearance = appear|look|im|pres|count|fac|fig|guis|sembl|complect|pal
//                                                                       |arousal|
! array sexuality = sex|feminin|masculin|homosex|lesb|bisex|man|woman|gend|ar

> topic physical includes global

    // [discuss] (some physical problem)
    + {weight=1000}[*]
    ^ (@respiratory|@urinary|@constipation|@eating|@indigestion|@mouth|@nausea|
    ^ @sleeping|@fatigue|@swelling|@fever|@walking|@concentration|@sensory|
    ^ @speaking|@appearance|@sexuality) [*]
    * <get physicalissue> ne None => {@ *}
    - <set physicalissue=<star1>>
    ^ <set counter=0>
    ^ Does the problem present itself in particular conditions?
    //^ Are you having trouble sleeping in general, or is there somthing in\s
    //^ particular that is preventing you from sleeping well?

    //follow up questions are asked twice before moving to the next topic
    //(keeping track of them through the counter uservariable)
    + *
    * <get counter> > 2 => I think that is enough information for the moment. {@ internal matcher to check if should make query}
    - How severe is your <get physicalissue> problem?<call>increase counter</call>
    - Have you often had similar problems in the past?<call>increase counter</call>
    - Is your <get physicalissue> problem affecting other areas of your life?<call>increase counter</call>
    // According to the documentation, <add counter=1> here could be used to
    // the same effect, however, an error is thrown with the present version
    // when this is used:
    // platform cygwin -- Python 2.7.10, pytest-2.9.2, py-1.4.31, pluggy-0.3.1
    // Rivescript version: 1.14.1
    // "TypeError: coercing to Unicode: need string or buffer, int found"

    + internal matcher to check if should make query
    * <call>shouldMakeQuery</call> == True => {topic=query}Before we move on would you like me to look for some information that might help with this?
    - <call>moveToNextTopic</call>

< topic

> topic query includes global

    + (@yes)
    - You can find more information here: {{ '<get microtopic>' }}<call>moveToNextTopic</call>

    + (@not)
    - <call>moveToNextTopic</call>

< topic
